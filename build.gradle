buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url 'https://jitpack.io' }
	}
}


plugins {
	id 'application'
	id 'uk.gov.hmcts.java' version '0.12.63'
	id 'jacoco'
	id 'pmd'
	id 'com.github.ben-manes.versions' version '0.51.0'//do not change this
	id "info.solidsoft.pitest" version '1.15.0'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.sonarqube' version '5.1.0.4882'
	id 'org.springframework.boot' version '3.3.2'
	id "org.flywaydb.flyway"version '8.5.13'
	id 'au.com.dius.pact' version '4.1.7' //do not change, otherwise serenity report fails
}

apply plugin: 'java'
apply plugin: 'project-report'
apply plugin: 'idea'

ext {
	springCloudVersion = '2023.0.3'
}

def versions = [
		junit              : '5.10.3',
		junitPlatform      : '1.9.2',
		lombok             : '1.18.32',
		pitest             : '1.16.1',
		reformLogging      : '6.0.1',
		reformS2sClient    : '5.1.0',
		springBoot         : '2.7.14',
		springHystrix      : '2.2.10.RELEASE',
		pact_version       : '4.6.11',
		rest_assured       : '4.2.1',
		log4j              : '2.23.1',
		jackson            : '2.17.2',
		feign              : '3.8.0',
		bouncycastle       : '1.78.1',
		testContainer_postgresql: '1.20.1'
]

mainClassName = 'uk.gov.hmcts.reform.profilesync.ProfileSyncApplication'
def jarName = 'rd-profile-sync.jar'

group = 'uk.gov.hmcts.reform.profilesync'
version = '0.0.1'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.80
			}
		}
	}
}

tasks.withType(Test) {
	useJUnitPlatform()

	testLogging {
		exceptionFormat = 'full'
	}
}

compileJava {
	options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

compileTestJava {
	options.compilerArgs << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(JavaExec).configureEach {
	javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

sourceSets {

	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integrationTest/java')
		}
		resources.srcDir file('src/integrationTest/resources')
	}

	functionalTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/functionalTest/java')
		}
		resources.srcDir file('src/functionalTest/resources')
	}

	smokeTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/smokeTest/java')
		}
		resources.srcDir file('src/smokeTest/resources')
	}

	contractTest {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
			srcDir file('src/contractTest/java')
		}
		resources.srcDir file('src/contractTest/resources')
	}
	pactTest {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
			srcDir file('src/pactTest/java')
		}
		resources.srcDir file('src/pactTest/resources')
	}
}

idea {
	module {
		testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
		testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
		testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
		testSourceDirs += project.sourceSets.functionalTest.resources.srcDirs
		testSourceDirs += project.sourceSets.smokeTest.java.srcDirs
		testSourceDirs += project.sourceSets.smokeTest.resources.srcDirs
	}
}


configurations {
	integrationTestImplementation.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
	functionalTestImplementation.extendsFrom testCompile
	functionalTestRuntime.extendsFrom testRuntime
	contractTestImplementation.extendsFrom testCompile
	contractTestRuntimeOnly.extendsFrom testRuntime
	pactTestImplementation.extendsFrom testCompile
	pactTestRuntime.extendsFrom testRuntime

}

check.dependsOn jacocoTestCoverageVerification

pmd {
	toolVersion = "6.55.0"
	ignoreFailures = true
	sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest, sourceSets.functionalTest, sourceSets.smokeTest]
	reportsDir = file("$project.buildDir/reports/pmd")
	ruleSets = [
			'category/java/errorprone.xml',
			'category/java/bestpractices.xml'
	]
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {

	systemProperty "scenario", System.getProperty('scenario')

	testClassesDirs = sourceSets.functionalTest.output.classesDirs
	classpath = sourceSets.functionalTest.runtimeClasspath
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
	testClassesDirs = sourceSets.smokeTest.output.classesDirs
	classpath = sourceSets.smokeTest.runtimeClasspath
}

task migratePostgresDatabase(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
	baselineOnMigrate = true
	if (project.hasProperty("dburl")) {
		url = "jdbc:postgresql://${dburl}"
	}
}

task contract(type: Test) {
	description = 'Runs the consumer Pact tests'
	useJUnitPlatform()
	testClassesDirs = sourceSets.contractTest.output.classesDirs
	classpath = sourceSets.contractTest.runtimeClasspath
	include "uk/gov/hmcts/reform/profilesync/**"
	systemProperty 'pact.rootDir', "pacts"
}

task runAndPublishConsumerPactTests(type: Test, description: 'Publish pact test to broker.', group: 'Verification') {
	// Nothing to do here
	testClassesDirs = sourceSets.contractTest.output.classesDirs
	classpath = sourceSets.contractTest.runtimeClasspath
}

task fortifyScan(type: JavaExec)  {
  mainClass.set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
  //This is a temporary fix to prevent the nightly build from failing if the Fortify scan detects issues
  ignoreExitValue = true
}

runAndPublishConsumerPactTests.finalizedBy pactPublish

pact {
	publish {
		pactDirectory = 'pacts'
		pactBrokerUrl = 'https://pact-broker.platform.hmcts.net'
		tags = ['Dev']
		version = "1.0"
	}
}

jacoco {
	toolVersion = "0.8.7"
}

jacocoTestReport {
	executionData(test, integration)
	reports {
		xml.required = true
		csv.required = false
		xml.outputLocation = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
	}
}

pitest {
	junit5PluginVersion = '1.2.1'
	targetClasses = ['uk.gov.hmcts.reform.profilesync.*']
	excludedClasses = [
			'uk.gov.hmcts.reform.profilesync.ProfileSyncApplication',
			'uk.gov.hmcts.reform.profilesync.config.*',
			'uk.gov.hmcts.reform.profilesync.client.*'
	]
	threads = 10
	outputFormats = ['XML', 'HTML']
	timestampedReports = false
	mutationThreshold = 80
	verbose = false
}

project.tasks['pitest'].group = "Verification"

sonarqube {
	properties {
		property "sonar.projectName", "rd-user-profile-sync-api"
		property "sonar.projectKey", "RDPROFILESYNCAPI"
		property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.outputLocation}"
		property "sonar.pitest.mode", "reuseReport"
		property "sonar.pitest.reportsDirectory", "build/reports/pitest"
		property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/profilesync/config/**," +
				"src/main/java/uk/gov/hmcts/reform/profilesync/client/**," +
				"src/main/java/uk/gov/hmcts/reform/profilesync/ProfileSyncApplication.java,"

	}
}

project.tasks['sonarqube'].dependsOn test, jacocoTestReport, integration
project.tasks['sonarqube'].group = "Verification"



repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

dependencies {

	configurations.all {
		exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
		exclude group: 'ch.qos.logback', module: 'logback-classic'
		exclude group: 'ch.qos.logback', module: 'logback-core'
	}

	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
	implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap'
	implementation ('org.springframework.boot:spring-boot-starter-security') {
		exclude module: "spring-boot-starter-logging"
		exclude module: "logback-classic"
	}
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'

	implementation (group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: versions.springHystrix){
		exclude group: "org.springframework.security"
	}
	implementation group: 'org.springframework.retry', name: 'spring-retry', version: '1.3.4'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'

	implementation group: 'org.springframework', name: 'spring-core'
	implementation group: 'org.springframework', name: 'spring-beans'
	implementation group: 'org.springframework', name: 'spring-aop'
	implementation group: 'org.springframework', name: 'spring-aspects'
	implementation group: 'org.springframework', name: 'spring-context'
	implementation group: 'org.springframework', name: 'spring-context-support'
	implementation group: 'org.springframework', name: 'spring-expression'
	implementation group: 'org.springframework', name: 'spring-jcl'
	implementation group: 'org.springframework', name: 'spring-orm'
	implementation group: 'org.springframework', name: 'spring-tx'
	implementation group: 'org.springframework', name: 'spring-web'
	implementation group: 'org.springframework', name: 'spring-webmvc'
	implementation group: 'org.springframework.security', name: 'spring-security-core'

	implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.1.8'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
	implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: versions.jackson, ext: 'pom'
	implementation group: 'io.github.openfeign.form', name: 'feign-form', version: versions.feign
	implementation group: 'io.github.openfeign.form', name: 'feign-form-spring', version: versions.feign

	implementation group: 'org.bouncycastle', name: 'bcpkix-jdk18on', version: versions.bouncycastle

	implementation group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '3.0.2'

	implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.8.0'

	implementation "com.github.hmcts.java-logging:logging:${versions.reformLogging}"
	implementation ('com.github.hmcts:service-auth-provider-java-client:5.1.3'){
		exclude group: "org.springframework.security"
	}

	implementation group: "com.google.code.gson", name: "gson", version: "2.11.0"

	implementation ('com.github.hmcts:idam-java-client:3.0.3'){
		exclude group: "org.springframework.security"
	}
	implementation "io.github.openfeign:feign-httpclient:11.10"
	implementation ('org.springframework.boot:spring-boot-starter-validation'){
		exclude group: 'org.glassfish', module: 'jakarta.el'
	}
	//Fix for CVE-2021-29425
	implementation 'commons-io:commons-io:2.16.1'
	implementation group: 'org.flywaydb', name: 'flyway-core',  version: '10.4.1'
	implementation group: 'org.flywaydb', name: 'flyway-sqlserver',  version: '10.4.1'
	implementation group: 'org.flywaydb', name: 'flyway-database-postgresql',  version: '10.4.1'
	implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.4'
	implementation group: 'com.google.guava', name: 'guava', version: '32.1.3-jre'

	implementation group: 'javax.el', name: 'javax.el-api', version: '3.0.0'
	
	implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: versions.log4j
  	implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: versions.log4j
  	implementation group: 'org.apache.logging.log4j', name: 'log4j', version: versions.log4j
  	implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: versions.log4j

	implementation group: 'com.microsoft.azure', name: 'applicationinsights-runtime-attach', version: '3.5.3'
	implementation group: 'com.microsoft.azure', name: 'applicationinsights-core', version: '3.5.3'

	compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
	annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
	testCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
	testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
	integrationTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
	integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
	functionalTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
	functionalTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
	smokeTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
	smokeTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

	testImplementation("org.hamcrest:hamcrest-junit:2.0.0.0") {
		exclude group: "org.hamcrest", module: "hamcrest-core"
		exclude group: "org.hamcrest", module: "hamcrest-library"
	}

	testImplementation 'net.serenity-bdd:serenity-rest-assured:3.9.8'
	testImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: '3.9.8'
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions.junit

	testImplementation(platform('org.junit:junit-bom:5.10.3'))
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
	testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
	testImplementation group: 'org.junit.platform', name: 'junit-platform-commons'
	testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine'

	testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.11.0'
	testImplementation group: 'org.mockito', name: 'mockito-inline', version: '3.12.4'
	testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.11.0'
	testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.9'

	testImplementation group: 'org.testcontainers', name: 'postgresql', version: versions.testContainer_postgresql
	testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: versions.testContainer_postgresql
	testImplementation group: 'org.testcontainers', name: 'testcontainers', version: versions.testContainer_postgresql

	testImplementation("com.github.tomakehurst:wiremock-jre8-standalone:2.35.2") {
		exclude group: 'com.github.jknack'
	}
	testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'

	testImplementation group: 'org.pitest', name: 'pitest', version: versions.pitest
	testImplementation 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.15.0'
	testImplementation 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'
	testImplementation group: 'org.pitest', name: 'pitest-junit5-plugin', version: '0.16'

	testImplementation 'com.github.hmcts:fortify-client:1.4.3:all'

	smokeTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
	smokeTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
	smokeTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
	smokeTestImplementation group: 'org.junit.platform', name: 'junit-platform-commons'
	smokeTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine'

	integrationTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
	integrationTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
	integrationTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
	integrationTestImplementation group: 'org.junit.platform', name: 'junit-platform-commons'
	integrationTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine'

	contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
	contractTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
	contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
	contractTestImplementation group: 'org.junit.platform', name: 'junit-platform-commons'
	contractTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine'
	contractTestImplementation('org.scala-lang:scala-library:2.13.14')

	contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5', version: versions.pact_version
	contractTestImplementation group: 'au.com.dius.pact.provider', name: 'spring', version: versions.pact_version
	contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5spring', version: versions.pact_version

	integrationTestImplementation sourceSets.main.runtimeClasspath
	integrationTestImplementation sourceSets.test.runtimeClasspath

	functionalTestImplementation sourceSets.main.runtimeClasspath
	functionalTestImplementation sourceSets.test.runtimeClasspath

	smokeTestImplementation sourceSets.main.runtimeClasspath
	smokeTestImplementation sourceSets.test.runtimeClasspath

	contractTestImplementation sourceSets.main.runtimeClasspath
	contractTestImplementation sourceSets.test.runtimeClasspath

	pactTestImplementation sourceSets.main.runtimeClasspath
	pactTestImplementation sourceSets.test.runtimeClasspath
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
	analyzers {
		// Disable scanning of .NET related binaries
		assemblyEnabled = false
	}
	suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyManagement {

	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}
dependencyUpdates.resolutionStrategy = {
	componentSelection { rules ->
		rules.all { ComponentSelection selection ->
			boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
				selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
			}
			if (rejected) {
				selection.reject('Release candidate')
			}
		}
	}
}

gradle.startParameter.continueOnFailure = true

bootJar {
	archiveFileName = jarName
	manifest {
		attributes('Implementation-Version': project.version.toString())
	}
}

configurations.all {
	resolutionStrategy.eachDependency { details ->
		if (details.requested.group == 'org.bouncycastle'
				&& (details.requested.name == 'bcpkix-jdk18on'
				|| details.requested.name == 'bcprov-jdk18on')
		){
			details.useVersion versions.bouncycastle
		}
	}
}

dependencies {
	modules {
		module("org.bouncycastle:bcpkix-jdk15on") {
			replacedBy("org.bouncycastle:bcpkix-jdk18on")
		}
		module("org.bouncycastle:bcprov-jdk15on") {
			replacedBy("org.bouncycastle:bcprov-jdk18on")
		}
	}
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues.
// this setting only applies when running via gradle bootRun -- see web.config for the
// java flag that configures the deployed appllications
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]


rootProject.tasks.named("processSmokeTestResources") {
	duplicatesStrategy = 'include'
}

rootProject.tasks.named("processFunctionalTestResources") {
	duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
	duplicatesStrategy = 'include'
}
